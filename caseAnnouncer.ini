[script]
n0=;Drill/Training Case Announcer
n1=;; Dialog config
n2=dialog cAnnounce {
n3=  title "Case Announcer Config"
n4=  size -1 -1 145 95
n5=  option dbu
n6=
n7=  ; controls:
n8=  text "Set the details for your case below...", 100, 5 4 120 10
n9=  text "Announcing to", 105, 5 12 120 10
n10=
n11=  check "Code Red?", 10, 5 20 47 10, left
n12=
n13=  text "System:", 101, 5 31 35 10
n14=  edit "", 11, 45 30 90 10, autohs
n15=
n16=  text "CMDR Name:", 102, 5 41 35 10
n17=  edit "", 12, 45 40 90 10, autohs
n18=
n19=  text "IRC Nick:", 103, 5 51 35 10
n20=  edit "", 13, 45 50 90 10, autohs limit 30
n21=
n22=  text "Platform:", 104, 5 61 35 10
n23=  combo 14, 45 60 60 10, sort drop
n24=
n25=  button "Send Case", 200, 5 78 45 12, ok
n26=  button "Cancel", 201, 55 78 45 12, cancel
n27=}
n28=
n29=;; Events
n30=; On Init
n31=on *:dialog:cAnnounce:init:*: {
n32=  did -ra $dname 105 Announcing to %ca_chan
n33=  did -ra $dname 12,13 $me
n34=  didtok $dname 14 44 PC,XB,PS
n35=  did -c $dname 14 1
n36=  did -f $dname 11
n37=  %ca_sendcase = $false
n38=}
n39=
n40=
n41=; on Click Send Case
n42=on *:dialog:cAnnounce:sclick:200 {
n43=  %ca_sendcase = $true
n44=}
n45=
n46=; On Click Cancel
n47=on *:dialog:cAnnounce:sclick:201 {
n48=  %ca_sendcase = $false
n49=}
n50=
n51=; on Close
n52=on *:dialog:cAnnounce:close:* {
n53=  ;get data
n54=  var %ca_cr = $did(10).state
n55=  var %ca_clNick = $did(13)
n56=  var %ca_clName = $did(12)
n57=  var %ca_sys = $did(11)
n58=  var %ca_plat = $did(14)
n59=  ; send annoucement
n60=  if (%ca_sendcase == $true) {
n61=    if (%ca_cr == 1) {
n62=      ;echo -ca info Sending message: !announce --cr %ca_chan $qt(%ca_clName) %ca_clNick %ca_plat %ca_sys
n63=      msg DrillSqueak[BOT] !announce --cr %ca_chan $qt(%ca_clName) %ca_clNick %ca_plat %ca_sys
n64=    } 
n65=    else {
n66=      ;echo -ca info Sending message: !announce %ca_chan $qt(%ca_clName) %ca_clNick %ca_plat %ca_sys
n67=      msg DrillSqueak[BOT] !announce %ca_chan $qt(%ca_clName) %ca_clNick %ca_plat %ca_sys
n68=    }
n69=  } 
n70=  else {
n71=    echo -ca info Cancelled annoucement.
n72=  }
n73=  ; clean up
n74=  unset %ca_*
n75=}
n76=
n77=alias cann {
n78=  if (#$1) { %ca_chan = $1 }
n79=  else { %ca_chan = # }
n80=
n81=  if (!%ca_chan) { 
n82=    echo -ca notice ERROR: /cann can only be run from a channel window.
n83=    halt 
n84=  }
n85=
n86=  ; don't allow announcing to main channels.
n87=  if ((%ca_chan == #fuelrats) || (%ca_chan == #ratchat)) {
n88=    var %str = You've attempted to target an announcement to %ca_chan $+ . Don't. $+ $crlf $+ $crlf $+ The annoucement has been cancelled.
n89=    $input(%str, oghdua, Annouce Failure)
n90=    unset %ca_*
n91=    halt
n92=  }
n93=
n94=  ;echo -ca info Sending drill case announce to $+(, %ca_chan, )
n95=  dialog -ma cAnnounce cAnnounce
n96=}
